plugins {
    id("java")
    id("xyz.jpenilla.run-paper") version("3.0.2")
    id("com.gradleup.shadow") version("9.2.2")
    id("de.eldoria.plugin-yml.bukkit") version("0.8.0")
}

group = 'io.github.acopier'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    implementation("com.cjcrafter:foliascheduler:0.7.2")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.8")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

bukkit {
    name = "RailDest"
    description = "A plugin that allows fully automatic rail junctions."
    version = "1.0-SNAPSHOT"
    website = "https://github.com/acopier/raildest"
    author = "acopier"
    main = "io.github.acopier.raildest.RailDestPlugin"
    foliaSupported = true
    apiVersion = "1.21"
}

tasks.shadowJar {
    archiveFileName.set("RailDest-${project.version}.jar")
    relocate("com.cjcrafter.foliascheduler", "io.github.acopier.raildest.schedulers")
}

processResources {
    def props = [version: version]
    inputs.properties(props)
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(props)
    }
}
